buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
            // Required for the curseforge task
            name = "gradle"
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.5"
    }
}

repositories {
	maven {
		name 'progwml6'
		url "http://dvs1.progwml6.com/files/maven/"
	}
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

ext.propFile = file "build.properties"

propFile.withReader {
	
	def prop = new Properties()
	prop.load(it)
	project.ext.props = new ConfigSlurper().parse prop
}

version = props.acb_version
group= "com.shinoow.acblocks" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AbyssalBlocks"

sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
    version = "1.9.4-12.17.0.1909-1.9.4"
    runDir = "run"
	
	useDepAts = true
	
	mappings = "snapshot_20160518"
	makeObfSourceJar = false
	
		replaceIn "AbyssalBlocks.java"
            replace "forgeversion", props.forge_version
}

dependencies {
	deobfCompile "mezz.jei:jei_1.9.4:3.4.0.202"
}

version = "${props.mc_version}-${props.acb_version}"

processResources
{
	// I really don't want that file to exist anywhere
	exclude '**/Thumbs.db'

	// this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
	
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
		include 'version.properties'

        // replace version and mcversion
        expand ([
			'version':project.version,
			'acbversion':project.props.acb_version,
			'mcversion':project.props.mc_version,
			'forgeversion':project.props.forge_version
		])
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
		exclude 'version.properties'
    }
}
	
task srcJar(type: Jar) {
    from sourceSets.main.allSource
	
	extension = 'jar'
	classifier = 'src'
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }

    extension = 'jar'
    classifier = 'dev'
}

task apiZip(type: Zip) {
		from(sourceSets.main.java){
		include 'com/shinoow/acblocks/api/**'
	}
	
	extension = 'zip'
	classifier = 'api'
}

	artifacts {
    archives srcJar, devJar, apiZip
}

curseforge {
    apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : '' // gradle curse -Pcurseforge_key=your-api-key
	project{
		id = "244465" // http://minecraft.curseforge.com/mc-mods/"xxxxxx"-projectname
		releaseType = project.props.release_type //alpha beta release
		changelogType = 'html'
		changelog = new File('changelog.html') //must have at least an empty string
		addArtifact devJar
		addArtifact srcJar
		addArtifact apiZip
		
		relations{
			requiredLibrary 'abyssalcraft'
		}
	}
}